
// Adapted from https://www.bennadel.com/blog/1504-ask-ben-parsing-csv-strings-with-javascript-exec-regular-expression-command.htm
// (JS code commented at bottom)

import "../Str.mls"
open Str

import "../Predef.mls"
open Predef


// The default delimiter is the comma, but this can be overriden here.
class CSV(strDelimiter) with ...


// Check to see if the delimiter is defined. If not, then default to comma.
set strDelimiter ||= ","

// TODO allow better code layout/formatting
val objPattern = new RegExp of
  // Delimiters.
  "(\\" + strDelimiter + "|\\r?\\n|\\r|^)" +
    // Quoted fields.
    "(?:\"([^\"]*(?:\"\"[^\"]*)*)\"|" +
      // Standard fields.
      "([^\"\\" + strDelimiter + "\\r\\n]*))"
  "gi"

// This will parse a delimited string into an array of arrays.
fun toArrays(strData) =
  
  // Create an array to hold our data. Give the array a default empty first row.
  let arrData = [[]]
  
  // Keep looping over the regular expression matches until we can no longer find a match.
  while
    let arrMatches = objPattern.exec(strData)
    arrMatches !== null do
      
      // Get the delimiter that was found.
      let strMatchedDelimiter = arrMatches.1
      
      // Check to see if the given delimiter has a length (is not the start of string)
      // and if it matches field delimiter.
      // If id does not, then we know that this delimiter is a row delimiter.
      if strMatchedDelimiter.length && strMatchedDelimiter != strDelimiter do
        // Since we have reached a new row of data, add an empty row to our data array.
        arrData.push([])
      
      // Now that we have our delimiter out of the way,
      // let's check to see which kind of value we captured (quoted or unquoted).
      let strMatchedValue =
        if arrMatches.2
        then
          // We found a quoted value. When we capture this value, unescape any double quotes.
          arrMatches.2.replace of
            new RegExp("\"\"", "g")
            "\""
        else
          // We found a non-quoted value.
          arrMatches.3
      
      arrData.at(arrData.length - 1).push(strMatchedValue)
  
  arrData


/*
  // This will parse a delimited string into an array of
  // arrays. The default delimiter is the comma, but this
  // can be overriden in the second argument.
  function CSVToArray( strData, strDelimiter ){
    // Check to see if the delimiter is defined. If not,
    // then default to comma.
    strDelimiter = (strDelimiter || ",");

    // Create a regular expression to parse the CSV values.
    var objPattern = new RegExp(
      (
        // Delimiters.
        "(\\" + strDelimiter + "|\\r?\\n|\\r|^)" +

        // Quoted fields.
        "(?:\"([^\"]*(?:\"\"[^\"]*)*)\"|" +

        // Standard fields.
        "([^\"\\" + strDelimiter + "\\r\\n]*))"
      ),
      "gi"
      );


    // Create an array to hold our data. Give the array
    // a default empty first row.
    var arrData = [[]];

    // Create an array to hold our individual pattern
    // matching groups.
    var arrMatches = null;


    // Keep looping over the regular expression matches
    // until we can no longer find a match.
    while (arrMatches = objPattern.exec( strData )){

      // Get the delimiter that was found.
      var strMatchedDelimiter = arrMatches[ 1 ];

      // Check to see if the given delimiter has a length
      // (is not the start of string) and if it matches
      // field delimiter. If id does not, then we know
      // that this delimiter is a row delimiter.
      if (
        strMatchedDelimiter.length &&
        (strMatchedDelimiter != strDelimiter)
        ){

        // Since we have reached a new row of data,
        // add an empty row to our data array.
        arrData.push( [] );

      }


      // Now that we have our delimiter out of the way,
      // let's check to see which kind of value we
      // captured (quoted or unquoted).
      if (arrMatches[ 2 ]){

        // We found a quoted value. When we capture
        // this value, unescape any double quotes.
        var strMatchedValue = arrMatches[ 2 ].replace(
          new RegExp( "\"\"", "g" ),
          "\""
          );

      } else {

        // We found a non-quoted value.
        var strMatchedValue = arrMatches[ 3 ];

      }


      // Now that we have our value string, let's add
      // it to the data array.
      arrData[ arrData.length - 1 ].push( strMatchedValue );
    }

    // Return the parsed data.
    return( arrData );
  }
*/


